apply plugin: 'com.android.application'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId 'com.skepticalone.armour'
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 7
        versionName "0.1.0"
        resConfigs "en"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        signingConfig signingConfigs.config
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        prerelease {
            initWith release
            applicationIdSuffix ".debug"
        }
        debug {
            initWith prerelease
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }
}

dependencies {
    //noinspection GradleDependency
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0-beta2'
    //noinspection GradleDependency
    annotationProcessor 'android.arch.lifecycle:compiler:1.0.0-beta2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    //noinspection GradleDependency
    implementation 'android.arch.lifecycle:extensions:1.0.0-beta2'
    //noinspection GradleDependency
    implementation 'android.arch.persistence.room:runtime:1.0.0-beta2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.threeten:threetenbp:1.3.6'
    //noinspection GradleDependency
    androidTestImplementation 'android.arch.persistence.room:testing:1.0.0-beta2'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support:support-annotations:26.1.0'
}
